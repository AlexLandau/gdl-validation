apply plugin: 'maven'
apply plugin: 'signing'

// Define how we publish artifacts to Maven.
// Derived from instructions on the Central Repository site:
// http://central.sonatype.org/pages/gradle.html
group = "net.alloyggp"
version = "0.2.1"
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}
uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'gdl-validation'
        packaging 'jar'
        artifactId = "gdl-validation"
        description 'A library for parsing and validating games in the Game Description Language (GDL).'
        url 'https://github.com/AlexLandau/griddle'

        scm {
          connection 'scm:git:git@github.com:AlexLandau/griddle.git'
          developerConnection 'scm:git:git@github.com:AlexLandau/griddle.git'
          url 'git@github.com:AlexLandau/griddle.git'
        }

        licenses {
          license {
            name 'The MIT License'
            url 'http://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'AlexLandau'
            name 'Alex Landau'
            email 'alex@alloyggp.net'
          }
        }
      }
    }
  }
}
